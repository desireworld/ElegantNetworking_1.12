buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
//        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
        classpath 'net.ltgt.gradle:gradle-apt-plugin:0.9'
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
    }
}

//apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'java'
apply plugin: 'net.ltgt.apt'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8
version = modVersion
group = modGroup
archivesBaseName = modBaseName

minecraft {
//    runDir = "run"
    mappings channel: 'stable', version: project.mcpVersion
}

repositories {
    mavenCentral()
    maven {
        name = "chickenbones"
        url = "https://chickenbones.net/maven" //or https://maven.covers1624.net/
    }
}

configurations {
    extraLibs
    runtime.extendsFrom extraLibs
    compile.extendsFrom extraLibs
}


sourceSets {
    main {
        resources {
            srcDir 'src/generated/resources'
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${project.forgeVersion}"
    compileOnly "codechicken:CodeChickenLib:1.12.2-3.2.3.358:universal"

//    compileOnly(project(':ElegantNetworkingCommon')) { transitive = false }
    extraLibs project(':ElegantNetworkingCommon')

    compileOnly 'org.projectlombok:lombok:1.18.8'
    annotationProcessor 'org.projectlombok:lombok:1.18.8', project(':ElegantNetworkingAnnotationProcessor')
}

shadowJar {
    classifier = ''
    configurations = [project.configurations.extraLibs]
    dependencies {}
}
shadowJar.finalizedBy('reobfJar')
compileJava {
    options.compilerArgs += [
            '-Aelegantnetworking.printDetails'
    ]
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.mc_version

    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        expand "version": project.version, "mcversion": project.mc_version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
    exclude('hohserg/elegant/networking/test/**')
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
    exclude('hohserg/elegant/networking/test/**')
}

artifacts {
    archives sourcesJar
    archives javadocJar
    archives shadowJar
}



// signing {
//     required { project.hasProperty("signing.keyId") }
//     sign configurations.archives
// }
publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = 'elegant-networking-1.12.2'
            version = project.version

            artifact sourcesJar
            artifact javadocJar
            artifact shadowJar
        }
    }
    repositories {
        maven {
            credentials {
                username = System.getenv('MAVEN_USER')
                password = System.getenv('MAVEN_PASSWORD')
            }
            name = 'remote'
            url = "https://nexus.tcoo.ru/repository/maven-releases/"
        }
    }
}